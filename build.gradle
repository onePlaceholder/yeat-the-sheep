plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.9-SNAPSHOT" apply false
    id "com.hypherionmc.modutils.modpublisher" version "2.1.6" apply false
    id "co.uzzu.dotenv.gradle" version "4.0.0"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "com.hypherionmc.modutils.modpublisher"
    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.officialMojangMappings()
    }

    publisher {
        apiKeys {
            curseforge env.CURSE_TOKEN.value
            modrinth env.MODRINTH_TOKEN.value
        }

        setCurseID rootProject.curseforge_id
        setModrinthID rootProject.modrinth_id
        setVersionType "release"
        setDisplayName "[$rootProject.minecraft_version - ${project.name.capitalize()}] Simple Banana - $rootProject.mod_version"
        setGameVersions rootProject.minecraft_version
        setLoaders([project.name])
        setCurseEnvironment "both"
        setVersion rootProject.mod_version
        setArtifact remapJar
        setJavaVersions([java.targetCompatibility])
        setChangelog rootProject.changelog
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {}

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 21
    }

    java {
        withSourcesJar()
    }
}
